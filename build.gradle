plugins {
    id 'java'
    id 'checkstyle'
    id 'application'
    id 'jacoco'
    alias libs.plugins.task.tree
}

group = 'io.huangsam'
version = '1.0.0'

repositories {
    mavenCentral()
}

dependencies {
    compileOnly libs.jetbrains.annotations
    implementation libs.bundles.google.all
    implementation libs.dropbox.sdk
    implementation libs.logback.classic
    implementation libs.metadata.extractor
    implementation libs.slf4j.api
    testCompileOnly libs.jetbrains.annotations
    testImplementation libs.bundles.mockito.all
    testImplementation platform(libs.junit.bom)
    testImplementation libs.junit.jupiter
    testRuntimeOnly libs.junit.launcher
}

application {
    mainClass = 'io.huangsam.photohaul.Main'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

check {
    dependsOn jacocoTestCoverageVerification
}

jacocoTestReport {
    reports {
        html.required = true
        xml.required = true
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: 'io/huangsam/**/Main.class')
        }))
    }
}

jacocoTestCoverageVerification {
    dependsOn jacocoTestReport
    violationRules {
        rule {
            limit {
                minimum = 0.7
            }
        }
    }
}
