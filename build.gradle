plugins {
    id 'java'
    id 'checkstyle'
    id 'application'
    id 'jacoco'
    alias libs.plugins.task.tree
}

group = 'io.huangsam'
version = '1.3.0'

repositories {
    mavenCentral()
}

dependencies {
    compileOnly libs.jetbrains.annotations
    implementation libs.bundles.google.all
    implementation libs.commons.net
    implementation libs.dropbox.sdk
    implementation libs.logback.classic
    implementation libs.metadata.extractor
    implementation libs.slf4j.api
    testCompileOnly libs.jetbrains.annotations
    testImplementation libs.bundles.mockito.all
    testImplementation platform(libs.junit.bom)
    testImplementation libs.junit.jupiter
    testRuntimeOnly libs.junit.launcher
}

application {
    mainClass = 'io.huangsam.photohaul.Main'
    // JVM performance tuning for photo processing workloads
    // -Xmx1g: Increase max heap size to 1GB for memory-intensive operations
    // -XX:+UseG1GC: Use G1 garbage collector for better performance with large heaps
    applicationDefaultJvmArgs = ['-Xmx1g', '-XX:+UseG1GC']
}

// Forward only the JVM system property -Dphotohaul.config to the app's JVM when provided.
// Default behavior (no -D) relies on Settings to use "config.properties".
tasks.named('run', JavaExec) {
    def override = System.getProperty('photohaul.config')
    if (override != null && override.toString().trim()) {
        systemProperty 'photohaul.config', override
        logger.lifecycle("[run] photohaul.config = ${override}")
    } else {
        logger.lifecycle('[run] photohaul.config not provided; using default config.properties')
    }
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

check {
    dependsOn jacocoTestCoverageVerification
}

jacocoTestReport {
    reports {
        html.required = true
        xml.required = true
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: 'io/huangsam/**/Main.class')
        }))
    }
}

jacocoTestCoverageVerification {
    dependsOn jacocoTestReport
    violationRules {
        rule {
            limit {
                minimum = 0.7
            }
        }
    }
}
